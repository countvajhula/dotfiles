;; Define evil states for each epistemic mode
(evil-define-state char
  "Char state."
  :tag " <X> "
  :message "-- CHAR --"
  ;;:cursor ;; inherit from normal
  ;;:entry-hook ;; potentially call the hydra here
  ;;:exit-hook ;; none
  ;;:suppress-keymap) ;; should be t, but probably inherits from normal
  :enable (motion normal))

(evil-define-state word
  "Word state."
  :tag " <W> "
  :message "-- WORD --"
  :enable (motion normal))

(evil-define-state line
  "Line state."
  :tag " <L> "
  :message "-- LINE --"
  :enable (motion normal))

(evil-define-state symex
  "Symex state."
  :tag " <λ> "
  :message "-- SYMEX --"
  :enable (motion normal))

(evil-define-state view
  "View state."
  :tag " <V> "
  :message "-- VIEW --"
  :enable (motion normal))

(evil-define-state window
  "Window state."
  :tag " <W> "
  :message "-- WINDOW --"
  :enable (motion normal))

(evil-define-state file
  "File state."
  :tag " <F> "
  :message "-- FILE --"
  :enable (motion normal))

(evil-define-state buffer
  "Buffer state."
  :tag " <B> "
  :message "-- BUFFER --"
  :enable (motion normal))

(evil-define-state application
  "Application state."
  :tag " <A> "
  :message "-- APPLICATION --"
  :enable (motion normal))

(evil-define-state system
  "System state."
  :tag " <S> "
  :message "-- SYSTEM --"
  :enable (motion normal))

(evil-define-state activity
  "Activity state."
  :tag " <A> "
  :message "-- ACTIVITY --"
  :enable (motion normal))

(evil-define-state mode
  "Mode state."
  :tag " <M> "
  :message "-- MODE --"
  :enable (motion normal))

(require 'my-char-mode)
(require 'my-word-mode)
(require 'my-line-mode)
(require 'my-symex-mode)
(require 'my-view-mode)
(require 'my-window-mode)
(require 'my-file-mode)
(require 'my-buffer-mode)
(require 'my-system-mode)
(require 'my-application-mode)
(require 'my-activity-mode)

;; define face for use in epistemic mode
(make-face 'eem-face)
(set-face-font 'eem-face "-*-Consolas-normal-normal-normal-*-*-*-*-*-m-0-iso10646-1")
(set-face-foreground 'eem-face "tomato")

(setq eem-complete-tower
      (ht ('name "complete")
          ('levels (ht ('0 (ht ('name "insert")
                               ('mode-entry 'evil-insert-state)))
                       ('1 (ht ('name "char")
                               ('mode-entry 'hydra-char/body)))
                       ('2 (ht ('name "word")
                               ('mode-entry 'hydra-word/body)))
                       ('3 (ht ('name "line")
                               ('mode-entry 'hydra-line/body)))
                       ('4 (ht ('name "view")
                               ('mode-entry 'hydra-view/body)))
                       ('5 (ht ('name "window")
                               ('mode-entry 'hydra-window/body)))
                       ('6 (ht ('name "file")
                               ('mode-entry 'hydra-file/body)))
                       ('7 (ht ('name "buffer")
                               ('mode-entry 'hydra-buffer/body)))
                       ('8 (ht ('name "system")
                               ('mode-entry 'hydra-system/body)))
                       ('9 (ht ('name "application")
                               ('mode-entry 'hydra-application/body)))))))

(setq eem-vim-tower
      (ht ('name "vim")
          ('levels (ht ('0 (ht ('name "insert")
                               ('mode-entry 'evil-insert-state)))
                       ('1 (ht ('name "normal")
                               ('mode-entry 'evil-normal-state)))))))

(setq eem-emacs-tower
      (ht ('name "emacs")
          ('levels (ht ('0 (ht ('name "emacs")
                               ('mode-entry 'evil-emacs-state)))))))

(setq eem-lisp-tower
      (ht ('name "lisp")
          ('levels (ht ('0 (ht ('name "insert")
                               ('mode-entry 'evil-insert-state)))
                       ('1 (ht ('name "symex")
                               ('mode-entry 'hydra-symex/body)))
                       ('2 (ht ('name "normal")
                               ('mode-entry 'evil-normal-state)))))))

(setq eem-towers
      (ht ('complete eem-complete-tower)
          ('lisp eem-lisp-tower)
          ('vim eem-vim-tower)
          ('emacs eem-emacs-tower)))

;; the prefix that will be used in naming all buffers used
;; in epistemic mode representations
(setq eem-buffer-prefix "EPISTEMIC")

(setq current-tower-index 0)
(make-variable-buffer-local 'current-tower-index)

(defun eem--tower (tower-id)
  "The epistemic tower corresponding to the provided index."
  (interactive)
  (nth tower-id (ht-values eem-towers)))

(defun eem--current-tower ()
  "The epistemic editing tower we are currently in."
  (interactive)
  (with-current-buffer eem--current-buffer
    (eem--tower current-tower-index)))

(defun eem-previous-tower ()
  "Previous tower"
  (interactive)
  (with-current-buffer eem--current-buffer
    (let ((tower-id (mod (- current-tower-index
                           1)
                        (ht-size eem-towers))))
     (eem--switch-to-tower tower-id))))

(defun eem-next-tower ()
  "Next tower"
  (interactive)
  (with-current-buffer eem--current-buffer
    (let ((tower-id (mod (+ current-tower-index
                           1)
                        (ht-size eem-towers))))
     (eem--switch-to-tower tower-id))))

(defun eem--switch-to-tower (tower-id)
  "Switch to the tower indicated"
  (interactive)
  (let ((tower (eem--tower tower-id)))
    (switch-to-buffer (eem--buffer-name tower))
    (with-current-buffer eem--current-buffer
      (setq current-tower-index tower-id))
    (eem--extract-selected-level)))

(defun eem--buffer-name (tower)
  "Buffer name to use for a given tower."
  (concat eem-buffer-prefix "-" (ht-get tower 'name)))

(defun eem--set-buffer-appearance ()
  "Configure mode mode appearance."
  (buffer-face-set 'eem-face)
  (text-scale-set 5)
  ;;(setq cursor-type nil))
  (hl-line-mode)
  (blink-cursor-mode -1)
  (internal-show-cursor nil nil)
  (display-line-numbers-mode 'toggle))

(defun eem--revert-buffer-appearance ()
  "Revert buffer appearance to settings prior to entering mode mode."
  (hl-line-mode -1)
  (blink-cursor-mode 1))

(defun eem-render-tower (tower)
  "Render a text representation of an epistemic editing tower."
  (interactive)
  (let ((tower-buffer (my-new-empty-buffer
                       (eem--buffer-name tower)))
        (tower-levels (ht-get tower 'levels)))
    (with-current-buffer tower-buffer
      (eem--set-buffer-appearance)
      (dolist (level-number
               (ht-keys tower-levels))
        (let ((level (ht-get tower-levels
                             level-number)))
          (insert "|―――"
                  (number-to-string level-number)
                  "―――|"
                  " " (ht-get level
                              'name) "\n")))
      (my-delete-line))
    tower-buffer))

(defun my-enter-mode-mode ()
  "Enter a buffer containing a textual representation of the
initial epistemic tower."
  (interactive)
  (setq eem--current-buffer (current-buffer))
  (dolist (tower (ht-values eem-towers))
    (eem-render-tower tower))
  ;; ideally we just switch to the buffer-local "current tower"
  ;; here, but for some reason it evaluates to the global value
  ;; when this function is invoked (but not before). We use a scratch
  ;; global value here to preserve the buffer-local value
  (eem--switch-to-tower eem--temp-tower-idx)
  (evil-mode-state))

(defun my-exit-mode-mode ()
  "Exit mode mode."
  (interactive)
  (eem--revert-buffer-appearance)
  (evil-normal-state)
  (kill-matching-buffers (concat "^" eem-buffer-prefix) nil t)
  ;; buffer-local workaround: buffer-local value is correct here,
  ;; so preserve it in a "scratch" global
  (setq eem--temp-tower-idx current-tower-index))

(define-key evil-insert-state-map [s-escape] 'eem-enter-higher-level)
(define-key evil-normal-state-map [s-escape] 'eem-enter-higher-level)
(define-key evil-normal-state-map [s-return] 'eem-enter-lower-level)

(defun eem--enter-level (level-number)
  "Enter level LEVEL-NUMBER"
  (let* ((tower (eem--current-tower))
         (levels (ht-get tower 'levels))
         (level (ht-get levels level-number)))
    (funcall (ht-get level 'mode-entry))
    (setq eem--current-level level-number)))

(defun eem-enter-selected-level ()
  "Enter selected level"
  (interactive)
  (eem--enter-level eem--selected-level))

(defun enter-first-level ()
  "Enter epistemic modes at first level"
  (interactive)
  (evil-force-normal-state)
  ;; start at the lowest level
  (eem--enter-level 1))

(defun eem-enter-lower-level ()
  "Enter lower level."
  (interactive)
  (eem--enter-level (- eem--current-level
                       1)))

(defun eem-enter-higher-level ()
  "Enter higher level."
  (interactive)
  (eem--enter-level (+ eem--current-level
                       1)))

(defun eem--extract-selected-level ()
  "Extract the selected level from the current representation"
  (interactive)
  (let* ((level-str (substring (thing-at-point 'line t) 0 5))
         (level-number (string-to-number (substring level-str 4 5))))
    (setq eem--selected-level level-number)))

(defun eem-select-previous-level ()
  "Select previous level"
  (interactive)
  (evil-previous-line)
  (eem--extract-selected-level))

(defun eem-select-next-level ()
  "Select next level"
  (interactive)
  (evil-next-line)
  (eem--extract-selected-level))

(defhydra hydra-mode (:idle 1.0
                      :columns 4
                      :body-pre (my-enter-mode-mode)
                      :post (my-exit-mode-mode))
  "Mode mode"
  ;; Need a textual representation of the mode tower for these to operate on
  ("h" eem-previous-tower "previous tower")
  ("j" eem-select-next-level "lower level")
  ("k" eem-select-previous-level "higher level")
  ("l" eem-next-tower "next tower")
  ;; ("H" eem-highest-level "first level (recency)")
  ;; ("J" eem-lowest-level "lowest level")
  ;; ("K" eem-highest-level "highest level")
  ;; ("L" eem-lowest-level "last level (recency)")
  ;; different towers for different "major modes"
  ;; ("s-o" eem-mode-mru "Jump to most recent (like Alt-Tab)" :exit t)
  ;; ("o" eem-mode-mru :exit t)
  ;; with delete / change etc. we could construct towers and then select towers
  ;; there could be a maximal tower containing all the levels
  ;; ("/" eem-search "search")
  ;; move to change ordering of levels, an alternative to recency
  ;;
  ;; ffap other window -- open file with this other mode/tower: a formal "major mode"
  ;; the mode mode, tower mode, and so on recursively makes more sense
  ;; if we assume that keyboard shortcuts are scarce. this gives us ways to use
  ;; a small number of keys in any arbitrary configuration
  ("<return>" eem-enter-selected-level :exit t)
  ("i" my-noop "exit" :exit t)
  ("<escape>" nil "exit" :exit t)
  ("s-<return>" eem-enter-lower-level "enter lower level" :exit t)
  ("s-<escape>" eem-enter-higher-level "escape to higher level" :exit t))

(global-set-key (kbd "s-m") 'hydra-mode/body)

(provide 'evil-epistemic-mode)
